plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'dagger.hilt.android.plugin'
    id 'kotlin-android-extensions'
    id 'androidx.navigation.safeargs.kotlin'
    id "kotlin-kapt"
    id 'com.diffplug.spotless'
}

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

def localPropertiesFile = rootProject.file("local.properties")
def localProperties = new Properties()
localProperties.load(new FileInputStream(localPropertiesFile))

def apiKey = localProperties['serviceApiKey']

android {
    compileSdk 31

    defaultConfig {
        applicationId "com.a1g0.tmdbclient"
        minSdk 21
        targetSdk 31
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        releasesigner {
            keyAlias keystoreProperties.releaseKeyAlias
            keyPassword keystoreProperties.releaseKeyPassword
            storePassword keystoreProperties.releaseStorePassword
            storeFile file('../release_key')
            v1SigningEnabled true
            v2SigningEnabled true
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.releasesigner
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "DATA_API_KEY","\"$apiKey\""
        }
        debug{
            buildConfigField "String", "DATA_API_KEY","\"$apiKey\""
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
        dataBinding true
    }
}

dependencies {

     // Android
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // Coroutines
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.2')
    implementation('org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.2')

    // Architecture Components
    implementation('androidx.lifecycle:lifecycle-viewmodel-ktx:2.4.1')
    implementation('androidx.lifecycle:lifecycle-livedata-ktx:2.4.1')

    // Room Components
    implementation('androidx.room:room-runtime:2.4.2')
    implementation('androidx.room:room-ktx:2.4.2')
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    kapt('androidx.room:room-compiler:2.4.2')

    // Coil-kt
    implementation('io.coil-kt:coil:1.4.0')

    // Retrofit
    implementation('com.squareup.retrofit2:retrofit:2.9.0')
    implementation('com.squareup.retrofit2:converter-moshi:2.9.0')
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    //OkHttp Logging
    def okhttpLoggingInterceptorVersion = '4.7.2'
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpLoggingInterceptorVersion"

    // Circle Image View
    implementation 'de.hdodenhof:circleimageview:3.1.0'

    // View Pager 2
    implementation 'androidx.viewpager2:viewpager2:1.0.0'

    // Moshi
    implementation("com.squareup.moshi:moshi-kotlin:1.11.0")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.11.0")
    implementation("com.squareup.moshi:moshi:1.11.0")

    // Hilt + Dagger
    implementation "com.google.dagger:hilt-android:$hilt_version"
    implementation  "androidx.hilt:hilt-lifecycle-viewmodel:1.0.0-alpha02"
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    kapt "androidx.hilt:hilt-compiler:1.0.0-alpha02"

    // Navigation Components
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.1'
    implementation 'androidx.navigation:navigation-ui-ktx:2.4.1'

    // Paging
    implementation "androidx.paging:paging-runtime:3.1.0"

    // Timber
    implementation 'com.jakewharton.timber:timber:5.0.1'

    //Skeleton View
    implementation 'com.faltenreich:skeletonlayout:4.0.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'

    //Youtube trailer player
    implementation 'com.pierfrancescosoffritti.androidyoutubeplayer:core:11.0.1'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}

kapt {
    correctErrorTypes true
}

apply from: "../spotless.gradle"